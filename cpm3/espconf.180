;
; configure the ESP-01 module
; this means to write the flash with
; network connection information
; the flash has to contain quotes for the names, but we
; make that completely transparent.

	include	mk4.lib

	extrn	itoa,atoi,strlen,cpdelim
	extrn	ismatch
	extrn	strcpy
	extrn	sreset
	extrn	wrtread
	extrn	espreset
	extrn	atcmd
	extrn	atcmdl
	extrn	hexdump
	extrn	hexdmps
	extrn	outstr
	extrn	hex2
	extrn	debugf
	extrn	retbuf

bdos	equ	0005h

start:
	ld	a,(080h)
	ld	(debugf),a

	ld	de,signon
	call	outstr

	call	sreset
	ld	de,resets
	jp	c,lose

	ld	a,0
	call	espreset
	ld	de,resete
	jp	c,lose

	ld	hl,cfrd
	ld	a,100+14+6
	call	atcmd
	ld	de,rdcm
	jp	c,lose

	ld	hl,retbuf+14		; save flash content
	ld	de,conf
	call	strcpy

	xor	a
	ld	(retbuf+14+100),a
	ld	(retbuf+14+8),a

	ld	hl,retbuf+14
	ld	de,magic
	call	ismatch
	jr	z,hazmagic

	ld	de,nomagic
	call	outstr

	call	inits

	jr	getconf

hazmagic:
	ld      hl,retbuf+14+8+1
        ld      de,ssid         ; get ssid
        call    qdelim
        ld      de,pass         ; get passwd
        call    qdelim
        ld      de,host         ; get host
        call    qdelim
        ld      de,port         ; get port
        call    cpdelim

	ld	de,currm
	call	outstr
	call	dumpit

getconf:
	ld	de,editm
	call	outstr

	ld	hl,ssidm
	ld	de,ssid
	call	prval

	ld	hl,passm
	ld	de,pass
	call	prval

	ld	hl,hostm
	ld	de,host
	call	prval

	ld	hl,portm
	ld	de,port
	call	prval

	ld	de,newm
	call	outstr
	call	dumpit

	ld	de,tbuf		; build config
	ld	hl,magic
	call	strcpy
	ld	a,':'
	call	apch
	ld	a,'"'
	call	apch
	ld	hl,ssid
	call	strcpy
	call	sep1
	ld	hl,pass
	call	strcpy
	call	sep1
	ld	hl,host
	call	strcpy
	ld	a,'"'
	call	apch
	ld	a,','
	call	apch
	ld	hl,port
	call	strcpy

	ld	de,tbuf
	call	strlen
	inc	hl		; include the null
	push	hl

	if	0
	ld	c,l
	ld	hl,tbuf
	call	hexdump
	endif

	ld	de,cmdbuf
	ld	hl,cfwr
	call	strcpy
	pop	hl
	push	hl
	call	itoa
	ld	hl,crlf
	call	strcpy
	ld	de,cmdbuf
	call	strlen

	if	0
	ld	c,l
	ld	hl,cmdbuf
	call	hexdump
	
	ld	hl,tbuf
	call	hexdmps
	ld	hl,conf
	call	hexdmps
	endif

	ld	hl,tbuf
	ld	de,conf
	call	ismatch
	jr	nz,dowrite
	ld	de,nochg
	pop	bc
	jp	win

dowrite:
	ld	hl,cfera
	ld	a,6
	call	atcmd
	ld	de,eram
	jp	c,lose

	ld	hl,cmdbuf
	ld	a,1
	call	atcmd
	ld	de,wrm
	jp	c,lose

	pop	bc
	ld	hl,tbuf
	ld	a,6
	call	atcmdl
	ld	de,wrm
	jp	c,lose

	ld	de,donem
win:
	call	outstr	
	ret

lose:	call	outstr
	ret

sep1:	ld	hl,del1
	call	strcpy
	ret

apch:	ld	(de),a
	inc	de
	ret

edit:	ld	de,tbuf
	call	outstr

	ld	de,tbuf
	call	strlen
	
eloop:	call	getch
	cp	ESC		; function keys
	jr	nz,chkbs
	call	getch
	cp	'['
	jr	nz,eloop
	call	getch
	cp	'A'
	jr	nc,eloop
tilde:	call	getch
	cp	'~'
	jr	nz,tilde
	jr	eloop
chkbs:	cp	DEL
	jr	z,bskey
	cp	BS
	jr	nz,chkcr
bskey:	ld	hl,tbuf
	sbc	hl,de
	jr	z,eloop

	ld	a,BS
	call	putch
	ld	a,SPACE
	call	putch
	ld	a,BS
	call	putch
	xor	a
	dec	de
	ld	(de),a
	jr	eloop	

chkcr:	cp	CR
	jr	nz,ichar
	call	putch
	ld	a,LF
	call	putch
	ret

ichar:	ld	(de),a
	call	putch
	inc	de
	xor	a
	ld	(de),a
	jr	eloop	
;
; getch: read a character from the keyboard to A
;
getch:	push	bc
	push	de
	push	hl
gpoll:	ld	c,6
	ld	e,0ffh
	call	5
	or	a
	jr	z,gpoll
	pop	hl
	pop	de
	pop	bc
	ret

putch:	push	bc
	push	de
	push	hl
	ld	c,6
	ld	e,a
	call	5
	pop	hl
	pop	de
	pop	bc
	ret
;
; edit buffer
; DE is buffer, HL is prompt
;
prval:	push	de
	push	de
	ex	de,hl
	call	outstr
	pop	hl
	ld	de,tbuf
	call	strcpy
	call	edit	
	pop	de
	ld	hl,tbuf
	call	strcpy
	ret
	
;
; do like cpdelim, only don't copy quotes.
; we do this by call cpdelim and stripping the quotes
; HL is the source, DE is destination
; we temporarily copy it to tbuf
;
qdelim::ld	b,d		; save the destination
	ld	c,e
	ld	de,tbuf
	call	cpdelim	
	ret	c		; if end of string
	push	hl		; save our source pointer

	dec	de		; points at the null
	ld	a,(de)
	cp	'"'
	jr	nz,notq		; not a trailing quote
	xor	a
	ld	(de),a		; make it a null
notq:	ld	de,tbuf
	ld	a,(de)
	cp	'"'
	jr	nz,notq1	; not a leading quote
	inc	de

	; now, DE is pointing into tbuf at our stripped value

notq1:	ld	h,b		; copy 
	ld	l,c	
	ex	de,hl
	call	strcpy		; copy HL to de
	pop	hl
	ret

dumpit:
	ld	de,ssidm
	call	outstr
	ld	de,ssid
	call	outstr
	ld	de,crlf
	call	outstr

	ld	de,passm
	call	outstr
	ld	de,pass
	call	outstr
	ld	de,crlf
	call	outstr

	ld	de,hostm
	call	outstr
	ld	de,host
	call	outstr
	ld	de,crlf
	call	outstr

	ld	de,portm
	call	outstr
	ld	de,port
	call	outstr
	ld	de,crlf
	call	outstr
	ret

inits:	
	ld	de,ssid
	ld	hl,ssid1
	call	strcpy

	ld	de,pass
	ld	hl,pass1
	call	strcpy

	ld	de,host
	ld	hl,host1
	call	strcpy

	ld	de,port
	ld	hl,port1
	call	strcpy
	ret

signon:	db	'ESP Flash Config',CR,LF,0

resets:	db	'Serial Reset Failed',CR,LF,0
resete:	db	'ESP Reset Failed',CR,LF,0
eram:	db	'Flash erase Failed',CR,LF,0
wrm:	db	'Flash write Failed',CR,LF,0
rdcm:	db	'Config read Failed',CR,LF,0

nomagic:db	'No stored configuration',CR,LF,0
currm:	db	'Current configuration: ',CR,LF,0
newm:	db	'New configuration: ',CR,LF,0
editm:	db	CR,LF,'Edit configuration: ',CR,LF,0
donem:	db	'Function complete',CR,LF,0
nochg:	db	'No change',CR,LF,0
ssidm:	db	'ssid: ',0
passm:	db	'password: ',0
hostm:	db	'host: ',0
portm:	db	'port: ',0

magic:  db      'DEADC0DE',0
del1:	db	'","',0
sep:    db      ':',0

cfera:  db      'AT+SYSFLASH=0,"mqtt_cert"',CR,LF,0
cfwr:   db      'AT+SYSFLASH=1,"mqtt_cert",0,',0
cfrd:   db      'AT+SYSFLASH=2,"mqtt_cert",0,100',CR,LF,0
dpr:    db      '>',0
crlf:   db      CR,LF,0
comma:  db      ',',0

ssid1:	db	'ssid',0
pass1:	db	'pass',0
host1:	db	'host',0
port1:	db	'9001',0

ssid:   ds      20              ; wifi ssid
pass:   ds      20              ;      password
host:   ds      20              ; cp/net server
port:   ds      5               ;      port

tbuf:	ds	256
cmdbuf:	ds	256
conf:	ds	256

	end
